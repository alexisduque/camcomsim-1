group 'fr.rtone.vlc'
version '1.0'
ext.githubProjectName = 'vlc-led2camera-simulator'

buildscript {
    ext.kotlin_version = '1.1.4'

    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        //maven { url 'http://jcenter.bintray.com' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.14.0"
        classpath 'com.mapvine:gradle-cobertura-plugin:0.1'
        classpath 'org.ajoberstar:gradle-git-publish:0.2.1'
    }

}


apply from: file('gradle/check.gradle')
apply from: file('gradle/convention.gradle')
apply from: file('gradle/license.gradle')

def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short' , 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

task cliJar(type: Jar, dependsOn:classes) {
    from files(sourceSets.main.output.classesDir)
    from {configurations.compile.collect {zipTree(it)}} {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }

    manifest {
        attributes 'Main-Class': 'fr.rtone.vlc.simulator.ui.SimulatorCLI'
        attributes 'Git-Version' : getVersionName()
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Built-By': System.getProperty('user.name'),
                'Built-JDK': System.getProperty('java.version'),
                'Build-Time': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Source-Compatibility': project.sourceCompatibility,
                'Target-Compatibility': project.targetCompatibility
        )

    }
    archivesBaseName = 'camcomsim'
    appendix = 'cli-' + new Date().format("MMddYYHHmm")
    version = rootProject.version + '-' + getVersionName()
}

jar {
    dependsOn cliJar
    manifest {
        attributes 'Git-Version' : getVersionName()
        attributes('Implementation-Title': project.name + '-lib',
                'Implementation-Version': project.version,
                'Built-By': System.getProperty('user.name'),
                'Built-JDK': System.getProperty('java.version'),
                'Build-Time': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Source-Compatibility': project.sourceCompatibility,
                'Target-Compatibility': project.targetCompatibility
        )

    }
    archivesBaseName = 'camcomsim'
    appendix = 'lib-' + new Date().format("MMddYYHHmm")
    version = rootProject.version + '-' + getVersionName()
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile "org.jgrapht:jgrapht-core:1.0.1"
    compile 'org.apache.commons:commons-lang3:3.5'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'
    compile 'org.apache.commons:commons-math3:3.6.1'
    compile 'com.google.code.gson:gson:2.2.4'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}